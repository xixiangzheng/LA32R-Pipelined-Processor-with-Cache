module RegFile(
    input                   [ 0 : 0]        clk,

    input                   [ 4 : 0]        rf_ra0,
    input                   [ 4 : 0]        rf_ra1,
    input                   [ 4 : 0]        rf_ra2,   
    input                   [ 4 : 0]        rf_wa,
    input                   [ 0 : 0]        rf_we,
    input                   [31 : 0]        rf_wd,

    output      reg         [31 : 0]        rf_rd0,
    output      reg         [31 : 0]        rf_rd1,
    output      reg         [31 : 0]        rf_rd2
);

reg [31 : 0] reg_file [0 : 31];

// 用于初始化寄存器
integer i;
initial begin
    for (i = 0; i < 32; i = i + 1)
        reg_file[i] = 0;
end

//读端口
always @(*) begin
    if (rf_ra0 == 0) begin
        rf_rd0 = 0;
    end
    else if (rf_we && rf_ra0 == rf_wa) begin
        rf_rd0 = rf_wd;     //保证写优先
    end
    else begin
        rf_rd0 = reg_file[rf_ra0];
    end
end

always @(*) begin
    if (rf_ra1 == 0) begin
        rf_rd1 = 0;
    end
    else if (rf_we && rf_ra1 == rf_wa) begin
        rf_rd1 = rf_wd;
    end
    else begin
        rf_rd1 = reg_file[rf_ra1];
    end
end

always @(*) begin
    if (rf_ra2 == 0) begin
        rf_rd2 = 0;
    end
    else if (rf_we && rf_ra2 == rf_wa) begin
        rf_rd2 = rf_wd;
    end
    else begin
        rf_rd2 = reg_file[rf_ra2];
    end
end

always @(posedge clk) begin         //写端口
    if (rf_we == 1 && rf_wa !=0)    //当且仅当写使能信号为1及所写端口号非0同时成立
        reg_file[rf_wa] <= rf_wd;
    else
        reg_file[rf_wa] <= reg_file[rf_wa];
end

endmodule